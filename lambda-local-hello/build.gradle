apply plugin: 'java'
apply plugin: 'war'
apply from: 'https://raw.github.com/akhikhl/gretty/master/pluginScripts/gretty.plugin'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    // lambda-local
    compile 'com.zaoo.lambda-local:lambda-local:0.1.25'

    // aws lambda
    compile 'com.amazonaws:aws-lambda-java-core:1.1.0'
    //compile 'com.amazonaws:aws-lambda-java-events:1.1.0'
    compile 'com.amazonaws:aws-lambda-java-log4j:1.0.0'

    // slf4j
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'org.slf4j:slf4j-log4j12:1.7.21' //including log4j
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
}

build.dependsOn buildZip

gretty {
    // supported values:
    // 'jetty7', 'jetty8', 'jetty9', 'tomcat7', 'tomcat8'
    servletContainer = 'jetty9'
    contextPath = ''
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}
